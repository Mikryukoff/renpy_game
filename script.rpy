# Игра начинается здесь:
label start:

    scene town

    '''Человек — удивительное существо. Каждый из нас вправе распоряжаться своей судьбой, ставить перед собой цели, искать пути их достижения и довольствоваться результатами.'''

    '''Каждый день судьбы людей переплетаются, мы невольно влияем на историю своей жизни, жизни людей вокруг. И лишь нам самим решать, как именно влиять на этот сценарий.''' 

    '''Быть ли в нем главным героем, приносящим добро и свет, или же просто плыть по течению бытия… А может, вам хочется быть злодеем?'''

    '''Эта история про одного маленького мальчика, хотя нет, это история в первую очередь про человека, который всё же сумел повлиять, достичь и воплотить…'''

# Первая сцена в комнате
    scene bedroom

    '*Стук в дверь*'

    show g_boy at right_pos
    with easeinright

    g_boy @ curious 'Мама, папа, вы вернулись?'

    show g_dad smile at left_pos1
    show g_mother at left_pos2
    with easeinleft
    
    g_mother @ talk 'Я и сама в это не верю, день выдался ужасно тяжелым! Еще и выходные оказались забиты...'

    # Диалоговое окно
    menu:
        'Что случилось? Мы же хотели на выходных вместе погулять в парке, там уточки…':
            jump asking_what_happened
        'Ничего страшного, раздевайтесь поскорее, будем обедать!':
            jump dont_asking

    return

# Не спрашиваем, что случилось
label dont_asking:
    
    g_dad @ smile_talk 'Погоди-ка, не только же плохие новости нам домой приносить. Лови!'

    jump show_pencils
    
    return

# Спрашиваем, что случилось
label asking_what_happened:
    
    g_mother @ talk_explain 'Извини, коллега на работе серьезно заболела, придется выйти за неё'

    g_dad @ talk 'Но мы обязательно найдем время и погуляем все вместе. Не расстраивайся!'

    g_mother @ talk 'Зато, смотри-ка, что у нас есть!'

    g_dad @ smile_talk 'Лови!'

    jump show_pencils

    return

# Дарим карандаши
label show_pencils:

    window hide
    show bg_dark with dissolve
    show pencils_export at show_item with easeintop

    menu:
        'Поймать карандаши':
            hide bg_dark
            hide pencils_export
            jump after_gift

        'Уронить карандаши':
            hide bg_dark
            hide pencils_export
            jump drop_pencils

    return

# Роняем карандаши
label drop_pencils:

    g_dad @ smile_talk 'Ну вот, надо бы нам с тобой потренироваться, в следующий раз на прогулку возьмём мячик!'

    jump after_gift

    return

# Продолжение после подарка
label after_gift:

    show g_dad smile_gift at left_pos1

    g_boy @ happy 'Вау! Откуда?! Они такие классные!'

    g_mother @ talk_explain 'Тебе подарок от нас с папой, знаем же, как ты любишь рисовать'

    g_dad @ smile_gift_talk 'Уж не знаю в кого ты так хорошо рисуешь, но раз так — талант нужно развивать!' 

    g_boy @ talk 'Здорово! Спасибо вам огромное, сейчас же их опробую!'

    g_mother @ talk 'Сначала пообедаем, а потом за дело!'

    g_boy @ happy 'Конечно! Хорошо! Бегу мыть руки!'

    jump scene_after_lunch

    return

# Сцена после ужина
label scene_after_lunch:

    scene work_zone
    
    g_boy 'Спасибо за ужин! Я буду у себя.'

    menu:
        'Достать карандаши':
            window hide
            show bg_dark with dissolve
            show pencils_export at show_item with easeintop

            g_boy 'Какие же вы красивые, наконец я смогу вас опробовать!'

            hide bg_dark
            hide pencils_export

            $ my_drawing = draw_logic.Draw.main(background='mini_game.png')

    scene bedroom
    show g_boy at right_pos
    show g_mother at left_pos2 with easeinleft

    g_mother @ talk 'Ну что, как успехи, уже попробовал?'

    g_boy @ happy 'Конечно! Смотри как здорово получилось.'

    show work_zone
    show expression my_drawing

    g_mother @ talk 'Ого! Как же здорово у тебя выходит! Этот рисунок так и веет радостью и уютом.' 

    g_mother @ talk_explain 'Не забрасывай своё хобби, я уверена, у тебя талант!'

    hide work_zone
    hide my_drawing

    g_boy @ curious 'Нет конечно, ты что! Я уже решил, хочу стать профессиональным художником, ну или дизайнером.'

    g_boy @ talk 'Вокруг нас всё такое серое, сплошная тоска и скука. Я хочу это исправить!'

    g_mother @ talk_explain 'Да, ты прав, наш город действительно мрачен и уныл, это сказывается и на его жителях.'

    g_boy @ talk 'Вот именно! Я хочу это исправить, хочу чтобы вам с папой жилось уютнее и радостнее!'

    g_mother @ talk 'Ах, какой же ты у нас заботливый!'

    g_mother @ talk 'Делай то, к чему лежит душа! А мы с папой обязательно тебя поддержим!' 

    g_mother @ talk_explain 'Но на сегодня советую закругляться, уже поздно.'

    g_boy @ talk 'Хорошо, спокойной ночи!'

    g_mother @ talk 'Спокойной ночи, сынок!'

    hide g_mother 
    with easeoutleft
    pause(0.5)

    show g_boy at left_pos2 with ease

    g_boy @ talk 'Пора спать.'

    scene bg_dark 

    g_boy 'Что жe... цель поставлена, осталось усердно трудиться и у меня всё получится!'

    g_boy 'Спи Сёма, тебе предстоит много работы, а пока нужно выспаться...'

    jump slide_show_scene

    return 

# Слайдшоу о том, что произошло
label slide_show_scene:

    scene town

    'Этот день стал точкой невозврата в судьбe нашего главного героя. Он выбрал весьма благородный, но тяжелый путь.'
    'Безусловно Сёму ждало множество препятствий, бывало даже возникали мысли, чтобы всё бросить.' 
    'Но поддержка родителей и нескончаемая мотивация дали свои плоды.'

    'Мальчик рос, а в месте с ним росли и его навыки. Он много трудился, учился, практиковался, при этом не забывая и про «счастливое детство».'

    menu:
        'Послушать о похождениях героя':
            hide bg_dark
            jump storylines

        'Пропустить похождения героя':
            hide bg_dark
            jump urfu_accepted

    return

# Развилки с предысторией
label storylines:
    if school or parents or painting or friends:
        
        scene town

        menu:
            'Школа' if school:
                scene class_room
                'Учеба в школе давалась нашему герою легко, он быстро «втягивался» в новые темы и показывал весьма достойные результаты.' 
                'Это было очень кстати, парню ведь в институт поступать, верно?'
                $ school = False
                jump storylines

            'Друзья' if friends:
                scene lecture_room
                'Общение с ребятами задалось не сразу, Сёма был весьма застенчив, а его интересы часто расходились с таковыми у сверстников.'
                'Однако спустя время парню всё же удалось найти компанию, в которой он чувствовал себя более чем комфортно.'
                $ friends = False
                jump storylines

            'Родители' if parents:
                scene future_bedroom
                'Родители поддерживали нашего героя во всем, искренне любили его и старались чаще проводить вместе время, несмотря на их тяжелую работу.'
                $ parents = False
                jump storylines

            'Рисование' if painting:
                scene work_zone
                'Сёма активно изучал различные тонкости рисования и дизайна, занимался на онлайн курсах и даже принимал участие в различных художественных конкурсах.' 
                'Желание мальчика связать свою жизнь именно с этой сферой из года в год лишь росло. Восхитительная целеустремленность!'
                $ painting = False
                jump storylines
    else:
        jump urfu_accepted

    return

# Сцена принятия в вуз
label urfu_accepted:

    scene future_bedroom
    show g_dad at left_pos1
    show g_mother at left_pos2

    '*стук в дверь*'

    show g_mc at right_pos with easeinright

    g_mother @ talk 'Ну чего ты так медлишь, говори быстрее, ну как, прошёл или нет?!'

    g_dad '...?!'

    menu:
        'Пошутить':
            jump to_joke
        'Сказать прямо':
            jump talk_true

    return

# Пошутить
label to_joke:

    g_mc @ hand_talk 'Послушайте, у меня есть 2 новости, хорошая и плохая, с какой мне начать?'

    g_mother @ talk_explain 'Сынок, ты издеваешься? Выкладывай поскорее, мы очень волнуемся!'

    g_mc @ talk 'Хорошо, тогда начну с плохой... {w}Мне придется уехать от вас на некоторое время...'

    g_dad @ talk 'Что значит уехать? Куда это ты собрался? А что же с результатами?'

    g_mother @ talk 'Да погоди, Семён, а что насчет хорошей новости?'

    g_mc @ smile 'Я прошёл!!!'
    
    g_mother @ talk_explain 'Я так и знала! Шутничок ты наш, зачем же так пугать! Я так рада!!!'

    jump boy_accepted

    return

# Говорим правду
label talk_true:

    g_mc @ smile 'Мои любимые родители, я с радостью сообщаю вам — я прошёл!!!' 

    g_mother 'Я так и знала! Я так рада!!!'

    jump boy_accepted

    return

# Исход вариантов с принятием в ВУЗ
label boy_accepted:

    g_dad @ smile_gift_talk 'Молодец! Ты просто молодец! Мы ни капли не сомневались в тебе. Как же ты нас обрадовал, сынок!'

    g_mc @ hand_talk 'Нужно потихоньку собираться, поезд уже послезавтра.'

    g_mother @ talk_explain 'Конечно, всё соберем, не беспокойся, пойду расскажу всем родственникам какой ты у нас умница!'

    g_dad @ smile_talk 'Не забудь забежать к бабушке, она очень переживает.'

    g_mc @ talk 'Да, конечно, она будет очень рада!'

    jump student_room

    return

# Комната в общаге
label student_room:

    scene dorm
    show g_mc at center

    g_mc @ hand_curious 'Ох, эти поезда очень утомляют, но нужно привыкать, теперь частенько кататься туда-сюда...'

    g_mc @ talk 'А здесь довольно уютно, рассчитывал на худшее.'

    menu:
        'Позвонить маме':
            jump call_mom
        'Разобрать вещи и лечь спать':
            jump to_sleep

    return

# Звоним маме
label call_mom:

    show g_mc phone at center

    g_mc @ phone_talk 'Ало, мамуль, всё хорошо, я добрался.'

    g_mother 'Отлично, как тебе комната?'

    g_mc @ phone_talk 'Вообще не плохо, признаться, думал всё будет куда печальнее.'

    g_mother 'Вот и здорово! Давай поскорее разбирай вещи и ложись, завтра у тебя уж очень насыщенный день, нужно выспаться!'

    g_mc @ phone_talk 'Это точно! Да, сейчас этим и займусь. Вы тоже ложитесь отдыхать, завтра вечером расскажу как всё прошло.'

    g_mother 'Хорошо, очень ждём звонка! Доброй ночи, сынок!'

    g_mc @ phone_talk 'Cпокойной ночи!'

    jump first_day_in_urfu

    return

# Разбираем вещи и ложимся спать
label to_sleep:

    g_mc @ talk 'Фух, это было не сложно, не так уж у меня и много вещей.'

    g_mc @ talk 'Звонить уже не буду, спят небось. Утром поболтаем.'

    g_mc '...'

    g_mc @ hand_talk 'Надеюсь, всё пройдёт хорошо, как-то неспокойно на душе.'

    g_mc '...'

    g_mc @ talk 'Ладно, нужно выспаться, день завтра обещает быть насыщенным...'

    jump first_day_in_urfu

    return

# Первый день в ВУЗе
label first_day_in_urfu:

    scene dark_scene
    show g_professor at center

    g_professor @ talk 'Дорогие первокурсники, я с радостью объявляю официальное начало вашей студенческой жизни! Ура!'
    
    hide g_professor with easeoutleft
    pause(3.0)
    show g_mc at right_pos with easeinright
    pause(2.0)
    show g_friend at left_pos2 with easeinleft

    g_friend @ talk 'Эй, приветик! Правда здорово?'

    menu:
        'Да, это точно!':
            jump friendly_choice
        
        'Не знаю, вроде ничего необычного.':
            jump unfriendly_choice

    return

# Отвечаем дружелюбно
label friendly_choice:

    g_friend @ smile 'Да! Думаю мы не зря выбрали УрФУ!'

    g_mc @ smile 'Поживём - увидим, однако мне тоже так кажется.'

    jump get_friend

    return

# Отвечаем угрюмо
label unfriendly_choice:
    
    g_friend @ smile 'Ты чего! Это же такой торжественный момент! Не зря же говорят, студенчество — лучшее время!'

    g_mc @ hand_talk 'Не могу похвастаться таким оптимизмом, но буду стараться.'

    jump get_friend

    return

# Продолжение знакомства
label get_friend:
    
    g_friend @ talk 'Ну вот! Я, кстати, - Денис, поступил на радиоэлектронику, хе-хе.{w} А что насчет тебя?' 

    g_mc @ hand_talk 'Я — Семён, на графическом дизайне, будем знакомы.'

    g_friend @ smile 'Славно! Кажется, церемония подходит к концу, пошли скорее осмотрим аудитории, может еще с кем познакомимся.' 

    g_mc @ talk 'Летс гоу!'

    jump studying_scenes

    return

# Сцены с учебой
label studying_scenes:

    scene class_room

    'Первый учебный день оказался позади, за ним — неделя, месяц, время уже подходило к первой в жизни Семёна сессии.' 
    
    'Но несмотря на успешную учебу, наш герой чувствовал, что что-то в его жизни изменилось. И явно не в лучшую сторону...'

    scene lecture_room

    'Нагрузка на парня возрастала, всё меньше времени у него оставалось для работы над своими проектами.' 

    'А ведь это была его отдушина, дело, в которое он вкладывал самого себя.'

    scene dorm

    'Мотивация парня всё активнее стремилась к нулю, ещё и предстоящие экзамены удручали его ситуацию.'

    'Однако всего один воодушевляющий диалог наконец пустил трещины в снежном коме грусти и тоски...'

    jump talking_with_teacher

    return

# Сцена с преподом
label talking_with_teacher:
    
    scene class_room
    show g_mc at right_pos
    show g_professor at left_pos1

    g_professor @ talk 'Послушай, Семён, нам нужно с тобой поговорить.'

    g_mc @ hand_talk 'Да, что такое, Виктор Степанович? Я с чем-то не справляюсь?'

    g_professor @ shocked 'Знаешь, в начале года ты был таким активным на занятиях, столько идей генерировал, всю работу на ура выполнял.' 

    g_professor @ surprised 'А сейчас что-то совсем раскис, с чего бы это?' 

    g_mc @ hand 'Извините, сам понимаю, что сильно расслабился...'

    g_mc @ talk 'Работы куча просто, много тягомотины, которая мне совсем не интересна, времени на свои проекты вообще не остаётся. {w}Еще и экзамены на носу.'

    g_professor @ shocked 'Ха, ну это дело обыденное, студенческая жизнь — она такая.'

    g_professor @ talk 'Я тебе вот что скажу, старайся проще относиться к обучению и получать из него максимум удовольствия, у тебя же сейчас такое прекрасное время..эх..как вспомню своё студенчество...'

    g_professor @ talk 'Пойми, сложности будут всегда, как и всегда тебе придётся иметь дело с тягомотной и абсолютно неинтересной работой, будь то нудная документация, или, как в твоём случае, неинтересные предметы.'

    g_mc @ smile 'Это точно, скажем так, я просто не привык к таким объёмам работы.'

    g_professor @ talk 'Понимаю, но всё равно не стоит отбрасывать свои интересы на второй план, старайся грамотно распределять время, всё же парень ты толковый.'

    g_mc @ hand_talk 'Хорошо, я постараюсь, спасибо большое!'

    g_professor @ shocked 'А теперь к делу, слушай, нашему факультету поступил заказ от ректора УРФУ: нужно сделать рекламный баннер для университета.'

    g_professor @ talk 'Именно поэтому я поручаю эту работу тебе и еще нескольким студентам, выбирал лично из наиболее способных.'

    g_professor @ shocked 'По истечению времени будет проведен конкурс, где мы и решим, чей баннер выбрать в качестве основного.' 

    g_professor @ talk 'Забыл сказать, даже если не победишь, но покажешь достойную работу — автомат по моему предмету обеспечен.'

    g_professor @ talk 'Ну, что думаешь?'

    menu:
        'Ну конечно я согласен!':
            jump accept_offer

        'Честно говоря, я не уверен, что найду время еще и на эту работу...':
            $ acception = False
            jump dont_accept

    return

# Соглашаемся сразу
label accept_offer:
    
    g_professor @ shocked 'Отлично! Значит я не ошибся с выбором!'

    g_professor @ 'Постарайся не загонять себя в строгие рамки, этот проект — замечательная возможность проявить творчество, так что дерзай!'

    g_professor @ talk 'А я всегда готов обсудить с тобой детали и помочь советом, так что не стесняйся обращаться. Мой номер и почта всегда к твоим услугам.'

    g_mc @ smile 'Хорошо, я обещаю очень постараться, еще раз большое спасибо!'

    g_mc @ hand_talk 'Честно, этот разговор был действительно важен для меня, за что я очень вам благодарен!'

    g_professor @ surprised 'Рад стараться, ну что, вперёд и с песней! Эх, а я потопал дальше возиться с «любимыми» бумажками...'

    g_mc @ smile 'Ахахах, успехов нам!'

    jump accept_dorm

    return

# Говорим, что подумаем
label dont_accept:
    
    g_professor @ surprised 'Чтож... ну я тебя не тороплю, хорошо подумай, однако как только решишь — сразу дай мне знать, мой номер у тебя есть, ну или на почту напиши.' 

    g_mc @ talk 'Да, хорошо, думаю сегодня-завтра точно дам ответ.'

    g_professor @ shocked 'Давай, парень, но помни, я не зря тебя выбрал и очень буду рад, если всё же поучаствуешь!'

    g_mc @ smile 'Хорошо, в любом случае большое спасибо за приятную беседу, вы здорово меня взбодрили!'

    g_professor @ talk 'Рад стараться! Ну-с, работа не ждёт, удачи!'

    g_mc @ smile 'До свидания!'

    jump accept_dorm

    return

# Cцена в общаге после согласия
label accept_dorm:

    scene dorm
    show g_mc at center 

    g_mc @ hand_talk 'Да уж, денёк выдался нелегким...'

    if acception:
        g_mc @ talk 'Ладно, всё, пора заканчивать своё «коматозное» состояние и приступить к делам с былым энтузиазмом!'

        g_mc @ hand_curious 'Осталось то всего ничего, сдать экзамены и наконец повидаться с родителями... Соскучился я прямо-таки не слабо, да и они, думаю, ждут меня с нетерпением!'

    else:
        g_mc @ talk 'Денёк был активным, надо бы еды приготовить человеческой, сколько можно травиться всякой дрянью из фастфудов.'

    '*стук в дверь*'

    g_mc @ talk 'Опа, кого это занесло? Время — вечер, ну ладно. {w}Открыто!'

    show g_mc at right_pos with ease
    show g_friend at left_pos1 with easeinleft

    g_friend @ smile 'Здорово, дружище!'

    g_mc @ smile 'Здоровей видали, ты чего-то поздновато, проходи.'

    g_friend @ talk 'Есть такое, да у меня самого вот только пары закончились, решил забежать поболтать, давненько не виделись.'

    g_mc @ hand_talk 'От беседы не откажусь, сам чуток нелюдимым стал, апатичным что ли...'

    g_friend @ smile 'А вот это ты зря! Раскис под самый конец, осталось-то зачеты получить да экзамены сдать, всего-то!'

    g_mc @ talk 'Да уж, «всего-то» , хорошая шутка...'

    g_friend @ talk 'Ну ладно тебе, прорвёмся, ты лучше расскажи, новости может какие есть, или истории интересные?'

    g_mc @ talk 'Ну, особо ничего нового, разве что...'

    jump actionlines

    return

# Меню выбора прошедших событий
label actionlines:

    menu:
        'Инженерная графика' if graphic:
            g_mc @ talk 'Вторую неделю с этой инженеркой маюсь, всё никак долги досдать не могу. Официально мой нелюбимый предмет! Но нужный, всё понимаю... {w}бесит зараза!'
            $ graphic = False
            jump actionlines
        
        'Родители купили кошку' if cat:
            g_mc @ smile 'О, точно, родители на днях решили кошку завести, мол привыкли заботиться о ком-то, а тут я уехал.'
            g_mc @ talk 'Назвали Маня, трёхцветная такая вся, но не породистая. Потом фотки скину. Насколько помню, папа в целом не очень кошек любит, однако к Мане быстро привык и стал её любимцем.'
            $ cat = False
            jump actionlines

        'Решил заняться спортом' if sport:
            g_mc @ hand_talk 'Ну, решил в форму потихоньку приходить, отжимания там, подтягивания, все дела.'

            g_mc @ talk 'Первые дни, конечно, совсем тяжко было, через силу себя заставлял. Но сейчас уже привык и даже в удовольствие, так и голова свежее для работы становится, и сам собой доволен.'
            $ sport = False
            jump actionlines

        'Рекламный баннер' if advert_banner:
            if acception:
                g_mc @ hand_talk 'Ах да, я и сам не успел толком переварить, но спешу тебе сообщить, Виктор Степанович предложил поучаствовать в конкурсе проектов: нужно сделать рекламный баннер для нашего университета.'

                g_mc @ smile 'Честно, у самого пока идей не особо, но дело меня сильно заинтересовало, к тому же ограничений шибко ни в чем нет, ну, разве что цензуру учитывать, само собой.'
            else:
                g_mc @ hand_talk 'Кстати, сегодня Виктор Степанович сделал мне неожиданное предложение — в конкурсе проектов поучаствовать, нужно разработать рекламный баннер для нашего университета.'

                g_mc @ talk 'Но я не знаю стоит ли браться за это дело, у меня ни времени особо нет лишнего, ни уверенности в своих силах, вдруг опозорюсь только, а оно мне надо?'
            $ advert_banner = False
            jump actionlines

    jump talking_with_friend

    return

# После рассказов о жизни
label talking_with_friend:
    
    g_friend @ smile 'Ну вот, дела у тебя идут даже насыщеннее моих!'

    if acception:
        g_friend @ talk 'Кстати, расскажи поподробнее по поводу баннера, это же очень круто! Когда собираешься приступить к наработкам? Может, я даже чем-нибудь помочь смогу.'

        g_mc @ hand_talk 'Да вот, желательно сегодня и приступить, хотя бы обдумать всё хорошенько и пару набросков сделать. К слову, помощь твоя мне еще понадобится, но детали обсудим по интернету.'

    else:
        g_friend @ talk 'К слову, насчет конкурса, ты чего это вздумал? Конечно участвуй! Это отличный шанс проявить творческие способности, плюс работа в твоё портфолио, что также не мало важно!'

        g_mc @ talk 'Да, но...'

        g_friend @ talk 'Никаких но! Поверь, если решишь не участвовать — потом будешь жалеть об упущенной возможности!'

        g_friend @ smile 'Я уверен, что получится у тебя очень здорово! Ну а я всегда готов помочь идеей или просто оценкой со стороны.'

        g_mc @ smile 'Ладно, ты прав, всё же стоит согласиться.'

    g_friend @ talk 'Вот и славно, вот и договорились! Ну что же, я пока пойду к себе, долги по учебе разгребать. Потом мне напишешь, обсудим детали.'

    g_mc @ smile 'Удачи!'

    if acception:
        hide g_friend with moveoutleft
        show g_mc at center with ease

        g_mc @ talk 'Думаю, мне стоит приступить к работе...'

        menu:
            'Достать карандаши':
                window hide
                show bg_dark with dissolve
                show pencils_export at show_item with easeintop

                g_mc 'Что же мне напоминает университет?'

                hide bg_dark
                hide pencils_export

                $ banner_drawing = draw_logic.Draw.main(background='mini_game.png')

                jump competition_result
    else:
        hide g_friend with moveoutleft
        show g_mc at center with ease
        show g_mc phone at center

        g_professor 'Ало, да, я слушаю.'

        g_mc @ phone_talk 'Здравствуйте, это Семён, я не поздно?'

        g_professor 'О, привет-привет, нет, я как раз только закончил с проверкой рефератов. {w}Ну, что надумал? Я ожидаю твердое «согласен»!'

        g_mc @ phone_talk 'Ха, ну может и не такое твёрдое, но да, я решил что буду участвовать.'

        g_professor 'Здорово! Честно, из всех участников я с нетерпением жду именно твою работу. Так что спасибо за радостные вести!'

        g_mc @ phone_talk 'Ну что же, в таком случае мне стоит сказать: «Я вас не подведу»!'

        g_professor 'Правильный настрой! Слушай, давай тогда на днях встретимся и обсудим все детали, разжуём идеи и просто поболтаем.'

        g_mc @ phone_talk 'Конечно!'

        g_professor 'Вот и отлично, тогда на сегодня всё, отдыхай и набирайся сил... ну или учись, если долгов много.'

        g_mc @ phone_talk 'К сожалению, второе, {w}спасибо, доброй вам ночи!'

        jump lets_drawing

        return

# Последнее решение
label lets_drawing:

        show g_mc at center

        g_mc @ talk 'Думаю, мне стоит приступить к работе... или всё-таки ну его...'

        menu:
            'Достать карандаши':
                window hide
                show bg_dark with dissolve
                show pencils_export at show_item with easeintop

                g_mc 'Что же мне напоминает университет?'

                hide bg_dark
                hide pencils_export

                $ banner_drawing = draw_logic.Draw.main(background='mini_game.png')

                $ acception = True
            
            'Не участвовать в конкурсе':
                g_mc 'Нет, у меня не получится, смысла стараться нет!'

                $ acception = False

                if error:
                    jump bad_end

                jump the_end

        jump competition_result

        return

# Результаты конкурса
label competition_result:
    
        scene color_town

        'Пролетели последние учебные дни 1-го семестра, экзамены уже позади, но перед тем, как наш главный герой вернется домой — нужно подвести итоги.'

        scene color_scene
        show rector at center

        rector @ talk 'Минуточку внимания, уважаемые студенты! Прошу тишины!'

        rector @ smile 'Итак, первый семестр завершен, с чем я вас торжественно поздравляю!'

        rector @ smile 'Впереди долгожданные новогодние каникулы, праздничное настроение, общение с родственниками и мандаринки, куда же без них!'

        rector @ talk 'Однако у нас есть одно незавершенное дело — сегодня мы выберем победителя в конкурсе проектов по созданию рекламного баннера для нашего университета!'

        rector @ talk 'Признаться, решить, какая же работа заслуживает называться лучшей, для нас было очень сложно, все участники постарались на славу, и их труд безусловно заслуживает уважение и вашего внимания!'

        rector @ talk 'Все работы будут вывешены на сайте нашего университета и транслироваться у входа в главное здание.'

        rector @ smile 'Но между тем, выбор сделать всё же пришлось. {w}Дамы и господа, я наконец торжественно объявляю победителя нашего сегодняшнего конкурса, — Семён!!!'

        rector @ talk 'Семён, прошу тебя выйти на сцену, если ты присутствуешь в зале!'

        show rector at left_pos1
        show mc smile at right_pos with easeinright

        rector @ talk 'Мои искренние поздравления и поздравления всего зала, ты проделал отличную работу, молодец!'

        mc @ hand_talk 'Извините, но я всё еще не могу поверить, что вы выбрали мою работу, я безмерно этому рад!'

        mc @ smile 'ПОБЕДА!!!'

        jump the_end

        return

# Конец
label the_end:

    if acception:
        scene color_town

        'С того момента прошло несколько лет, несколько счастливых, наполненных как тяжелым трудом и самосовершенствованием, так и незабываемыми моментами студенческой жизни.'

        'Учеба в университете подходила к концу, дела у нашего героя шли только в гору, взбираясь по которой, шаг за шагом, он становился всё ближе к своей цели.'

        scene color_bedroom
        show mc at center

        mc @ smile 'Пожалуй, всё же представлюсь.'

        mc @ talk 'Победа на конкурсе словно вдохнула в меня новые, совершенно неощутимые ранее силы.'

        mc @ hand_talk 'Я стал увереннее относится к себе, к своему творчеству и способностям, хватался за любую возможность проявить себя и улучшить навыки.'

        mc @ hand_talk 'Стоит ли говорить, что жизнь заиграла новыми красками, учеба перестала так тяготить, оставляя предостаточно времени для собственных проектов.'

        mc @ smile 'И тем не менее я понимаю, какой длинный и насыщенный меня ждёт путь дальше.'

        mc @ talk 'Жизнь — коварна и беспощадна — считают многие, однако не стоит искать и добиваться её пощады, куда лучше найти в себе силы преодолеть её препятствия и держать под контролем, не так ли?'

        mc @ hand_talk 'Что же, надеюсь вам понравилась эта скромная история. Вы можете возмутиться: как же так, ведь в начале нам четко сказали, что главный герой всё же смог, он достиг своей цели!?'

        mc @ smile 'Ну, что поделать, теперь я точно не могу всё бросить и подвести вас, верно?'

        mc @ talk 'Вперёд и с песней, до встречи!'

        return

    else:
        scene town

        'С того момента прошло несколько лет, наш герой погряз в работе и потерял веру в себя.'

        'Отказ от участия в конкурсе оказался фатальной ошибкой, и наш герой не справился с давлением и виной, которые нависли над ним.'

        'Весь мир остался серым и неуютным, а лень победила мечту детства.'

        scene bedroom
        show g_mc at center

        g_mc @ talk 'Пожалуй, всё же представлюсь.'

        g_mc @ talk 'К сожалению, моя жизнь пошла по не тому пути, о котором я мечтал.'

        g_mc @ hand_talk 'И всё же у меня есть шанс исправить ошибку, потому что я герой игры, а здесь возможно всё.'

        menu:
            'Исправить ошибку':
                $ error = True 
                jump lets_drawing

            'Принять судьбу':
                jump bad_end
        return

# Плохая концовка(За неё топил только разработчик, вся вина на нём)
label bad_end:

    scene bedroom
    show g_mc at center 

    g_mc @ hand_talk 'Что же... оставим всё как есть, жизнь на этом не заканчивается.'

    g_mc @ talk 'Спасибо, что выслушали мою историю, следуйте своей мечте и хватайтесь за любую возможность её исполнить, до встречи!'

    return
